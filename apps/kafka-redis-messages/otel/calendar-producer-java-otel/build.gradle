/*
 * Distributed Calendar Application - OTel variant (pure OpenTelemetry instrumentation).
 *
 * This variant uses the OTel Java agent for auto-instrumentation of Spring Web,
 * Redis (Jedis), and Kafka. Additionally, the OTel Kafka client library is used
 * for programmatic Kafka producer wrapping to inject trace context into messages.
 *
 * The OTel agent is attached via -javaagent JVM flag in the Dockerfile.
 * Traces are exported via OTLP to the Datadog Agent.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'application'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.5'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Redis client - OTel agent auto-instruments Jedis
    implementation 'redis.clients:jedis:5.1.2'

    // Logging
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'ch.qos.logback:logback-core:1.4.14'
    implementation 'org.slf4j:slf4j-api:2.0.12'

    // Kafka client
    implementation 'org.apache.kafka:kafka-clients:3.7.0'

    // OTel API and SDK for manual instrumentation
    implementation 'io.opentelemetry:opentelemetry-api:1.36.0'
    implementation 'io.opentelemetry:opentelemetry-sdk:1.36.0'

    // OTel Kafka instrumentation library for wrapping KafkaProducer
    // This injects W3C Trace Context headers into Kafka messages,
    // enabling distributed tracing across Kafka.
    implementation 'io.opentelemetry.instrumentation:opentelemetry-kafka-clients-2.6:2.2.0-alpha'
}

group = 'com.datadog.example'
version = '0.0.1-SNAPSHOT'
description = 'calendar'
java.sourceCompatibility = 17
mainClassName = 'com.otel.main.SpringApp'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
